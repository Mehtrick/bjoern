buildscript {
 	ext{
        if (!project.hasProperty("nexusUser")) {
            nexusUser = ""
        }
        if (!project.hasProperty("nexusPW")) {
            nexusPW = ""
        }
        nexusUsername = "${nexusUser}"
        nexusPassword = "${nexusPW}"
        publishRepo = "release"
        nexusBasePath = "https://nexus.ppvoting.de"
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.kageiit:url-cache-plugin:1.0.0"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}


allprojects{
    group = 'de.mehtrick.bjoern'
    apply plugin: 'java-library'
    apply plugin: 'eclipse'
    apply plugin: 'com.kageiit.url-cache'
    apply plugin: 'maven'
    apply plugin: 'signing'
    repositories {
        jcenter()
        maven{
    	    url = "https://nexus.ppvoting.de/content/repositories/release/"
        }
    }
    compileJava.options.encoding = 'UTF-8'
}


subprojects {
    version = '1.0.4'
    dependencies {
        implementation 'org.apache.commons:commons-lang3:3.7'
        implementation 'commons-io:commons-io:2.6'
        implementation 'org.apache.commons:commons-text:1.4'
        implementation 'de.mehtrick:umloud:1.0.0'
        implementation 'org.slf4j:slf4j-simple:1.7.26'
        compileOnly 'org.projectlombok:lombok:1.18.0'

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.assertj:assertj-core:3.10.0'
    }

    //apply from: "https://bitbucket.org/Mehtrick/gradle-module/raw/0c379000383ebb1578691925f2a9be39698aef9a/backend/publish%2Brelease.gradle"

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives javadocJar, sourcesJar
    }

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                snapshotRepository(url: " https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                pom.project {
                    name 'Bjoern Testing Framework'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'Simple bdd-java-framework for universal unit testing'
                    url 'https://github.com/Mehtrick/bjoern'

                    scm {
                        connection 'scm:git:https://github.com/Mehtrick/bjoern.git'
                        url 'https://github.com/Mehtrick/bjoern'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://github.com/Mehtrick/bjoern/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'mehtrick'
                            email 'p.lauhof@gmail.com'
                        }
                    }
                }
            }
        }
    }

}



